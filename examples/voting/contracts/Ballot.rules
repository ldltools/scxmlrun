// Ballot.rules
// this module fairly simulates what Ballot.sol does.
// - it can replace Ballot.sol and run as a smart contruct on ethereum when combined with proxy.sol.
// - it can coexist with Ballot.sol and run as a monitor for Ballot.sol.
//
// EVENTS
// - Propose (nproposal)
// - GiveRightToVote (voter)
// - Vote (proposal)
// - Delegate (voter)
// - _success
//   internal event that indicates its preceding Vote/Delegate event
//   has been processed successfully
// - error.execution
//   this indicates a runtime error.
//   Notice that this appears only within "{..}" blocks.
//

protocol
  Propose;
  GiveRightToVote;
  (GiveRightToVote + Delegate; _success + Vote; _success)*;
  (Delegate + Vote); _success
;;

rule

// Propose (int nproposal)
on Propose
do
{
  nproposal = _event.data.nproposal;
  var i; for (i = 0; i < nproposal; i++) proposals[i] = {voteCount:0};
  chairperson = _event.origin;
  voters[chairperson] = {weight : 1, voted : false, delegate : "0", vote : 0};
  nvoter = 0;
  console.log ("chair=", chairperson, "nproposal=", nproposal);
}

// GiveRightToVote (address from, address to)
on GiveRightToVote
do
{
  if (_event.origin != chairperson)
    SCXML.raise ({name : "error.execution"});

  var voter_addr = _event.data.voter; // string
  voters[voter_addr] = {weight : 1, voted : false, delegate : "0", vote : 0};
  nvoter++;
}

// Vote (int proposal)
on Vote
raise _success
{
  var voter_addr = _event.origin;
  if (voters[voter_addr] == undefined || voters[voter_addr].voted)
    SCXML.raise ({name : "error.execution"});
  console.assert (!voters[voter_addr].voted);

  var proposal = _event.data.proposal;
  voters[voter_addr].voted = true;
  voters[voter_addr].vote = proposal;
  proposals[proposal].voteCount += voters[voter_addr].weight;
  nvoted += voters[voter_addr].weight;

  SCXML.raise ({name : "_success"});

  console.log ("Vote: ", _event.origin, _event.data.proposal, "nvoted=", nvoted, "/", nvoter);
  if (nvoted == nvoter) {
    var i;
    for (i = 0; i < nproposal; i++) console.log ("proposals[", i, "]=", proposals[i].voteCount);
  }
}

// Delegate (address voter)
on Delegate
raise _success
{
  var sender_addr = _event.origin;
  if (voters[sender_addr] == undefined || voters[sender_addr].voted)
    SCXML.raise ({name : "error.execution"});
  var voter_addr = _event.data.voter;
  if (voters[voter_addr] == undefined || voter_addr == sender_addr)
    SCXML.raise ({name : "error.execution"});
  console.assert (voter_addr != sender_addr);

  while (voters[voter_addr].delegate != "0")
  {
    voter_addr = voters[voter_addr].delegate;
    if (voter_addr == sender_addr) SCXML.raise ({name : "error.execution"});
  }

  voters[sender_addr].voted = true;
  voters[sender_addr].delegate = voter_addr;

  if (voters[voter_addr].voted)
  {
    proposals[voters[voter_addr].vote].voteCount += voters[sender_addr].weight;
    nvoted += voters[sender_addr].weight;
  }
  else
  {
    voters[voter_addr].weight += voters[sender_addr].weight;
  }

  SCXML.raise ({name : "_success"});

  console.log ("Delegate: ", _event.origin, _event.data.voter, voters[sender_addr].weight, "nvoted=", nvoted, "/", nvoter);
  if (nvoted == nvoter) {
    var i;
    for (i = 0; i < nproposal; i++) console.log ("proposals[", i, "]=", proposals[i].voteCount);
  }
}

// implementation-specific
script
{
  <datamodel>
    <data id="proposals" expr="[]"/>
    <data id="nproposal" expr="0"/>
    <data id="voters" expr="{}"/>
    <data id="nvoter" expr="0"/>
    <data id="nvoted" expr="0"/>
    <data id="chairperson" expr=""/>
  </datamodel>
}
